plugins {
    id "net.minecraftforge.gradle.forge" version "2.0.2"
    id "com.matthewprenger.cursegradle" version "1.0.3"
    id "maven-publish"
}

ext.configFile = file "build.properties"
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

def secretFile = file "secret.properties"
secretFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.secret = new ConfigSlurper().parse prop
}

version = config.mod_version + "-" + config.minecraft_version
group = config.mod_group
archivesBaseName = config.mod_name
sourceCompatibility = targetCompatibility = 1.6

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    runDir = "minecraft"
    mappings = "snapshot_20141130"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version":project.version, "mcversion":project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    manifest {
        attributes "FMLAT" : "llibrary_at.cfg"
    }
}

task devJar(type: Jar) {
    classifier = "dev"
    from sourceSets.main.output
    manifest {
        attributes "FMLAT" : "llibrary_at.cfg"
    }
}

artifacts {
    archives devJar, sourceJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "net.ilexiconn"
            artifactId "llibrary"
            version config.mod_version + "-" + config.minecraft_version

            from components.java

            artifact devJar {
                classifier "dev"
            }

            artifact sourceJar {
                classifier "sources"
            }

            pom.withXml {
                asNode().appendNode("description", "The modding library that claims to be lightweight")
                asNode().appendNode("url", "https://github.com/iLexiconn/LLibrary")
            }
        }
    }
    repositories {
        maven {
            credentials {
                username secret.username
                password secret.password
            }
            url secret.repo
        }
    }
}

import groovy.json.JsonSlurper
curseforge {
    debug = true

    def url = "https://github.com/iLexiconn/LLibrary/raw/version/versions.json".toURL()
    def versions = new JsonSlurper().parseText url.text

    def type = "release"
    def latestVersion = ""
    versions.versions.each{version, changelog -> latestVersion = version}
    if (versions.beta == latestVersion) {
        type = "beta"
    }

    apiKey = secret.curseForgeApiKey

    project {
        id = "230798"

        releaseType = type
        changelog = versions.versions[versions.getAt(type)].join("\n")

        addGameVersion config.minecraft_version

        mainArtifact(jar) {
            displayName = "LLibrary " + config.mod_version + " for Minecraft " + config.minecraft_version
        }

        addArtifact(devJar) {
            displayName = "Development"
        }

        addArtifact(sourceJar) {
            displayName = "Sources"
        }
    }
}
